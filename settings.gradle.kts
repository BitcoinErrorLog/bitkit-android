import java.util.*

val localProperties by lazy {
    Properties().apply {
        val file = rootDir.resolve("local.properties")
        if (file.exists()) {
            file.inputStream().use { load(it) }
        }
    }
}
fun getGithubCredentials(
    passKey: String = "gpr.key",
    userKey: String = "gpr.user",
): Pair<String?, String?> {
    val user = System.getenv("GITHUB_ACTOR")
        ?: providers.gradleProperty(userKey).orNull
        ?: localProperties.getProperty(userKey)
    val key = System.getenv("GITHUB_TOKEN")
        ?: providers.gradleProperty(passKey).orNull
        ?: localProperties.getProperty(passKey)
    return user to key
}
pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
@Suppress("UnstableApiUsage")
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/synonymdev/bitkit-core")
            credentials {
                val (user, pass) = getGithubCredentials()
                username = user
                password = pass
            }
        }
        maven {
            url = uri("https://maven.pkg.github.com/synonymdev/vss-rust-client-ffi")
            credentials {
                val (user, pass) = getGithubCredentials()
                username = user
                password = pass
            }
        }
        maven("https://jitpack.io")
    }
}
rootProject.name = "bitkit-android"
include(":app")
